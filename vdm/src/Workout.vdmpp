class Workout
/**
Class that represent a workout (run).
Workouts have a route and can be of 2 types.
A WorkoutExisting is a workout where the users
follows an already known route.
A WorkoutNew is when a user runs a new route. 
**/
instance variables

	-- The workout's route
	protected route: Types`Route;
	-- The starting timestamp
	protected startTime: Types`Timestamp;
	-- The ending timestamp
	protected endTime: Types`Timestamp;
	-- The threshold is a natural number representing
	-- the minimum distance from a previous point needed
	-- in order to be considered a new point to the route
	protected threshold: nat;
	
	-- The starting time cannot be after the ending time
	inv Math`timeToSeconds(startTime) <= Math`timeToSeconds(endTime);
	
operations
	
	--Check if a workout is finished
	public isFinished: () ==> bool
	isFinished() == is subclass responsibility;
	
	--Receives a new GPS Position
	public newGPSPosition: Types`Point ==> ()
	newGPSPosition(point) == is subclass responsibility;
	
	--Returns a distance
	public getDistance: () ==> nat
	getDistance() == is subclass responsibility;
	
	--Returns last point
	public getLastPoint: () ==> [Types`Point]
	getLastPoint() == is subclass responsibility;
	
	--Returns the route of an task
	public getRoute: () ==> Types`Route
	getRoute() == 
	(
		return route;
	);
	
	--Sets the time to finish the circuit
	public finish: Types`Timestamp ==> ()
	finish (time) ==
	(
		endTime := time
	)
	pre Math`timeToSeconds(time) >= Math`timeToSeconds(startTime)
	post endTime = time;
	
	-- Calculates the average velocity 
	public calculateVelocity: () ==> real
	calculateVelocity() == return getDistance() / Math`timeToSeconds(calculateTime())
	post endTime = endTime~ and startTime = startTime~;
	
	-- Calculates the duration of the workout
	public calculateTime: () ==> Types`Timestamp
	calculateTime() ==
	(
		return Math`secondsToTime(abs (Math`timeToSeconds(endTime) - Math`timeToSeconds(startTime)));
	)
	post endTime = endTime~ and startTime = startTime~;
	
	--Checks if a given point is outside of the threshold
	protected outsideOfThreshold: Types`Point ==> bool
	outsideOfThreshold (point) ==
	(
		if getLastPoint() = nil then
			return false;
		return Math`distance(point, getLastPoint()) > threshold;
	);
	
	--Checks if a given point is inside of the threshold
	protected insideOfThreshold: Types`Point ==> bool
	insideOfThreshold (point) ==
	(
		if getLastPoint() = nil then
			return false;
		return Math`distance(point, getLastPoint()) <= threshold;
	);
	
end Workout