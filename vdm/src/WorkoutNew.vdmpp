class WorkoutNew is subclass of Workout
/**
Subclass WorkoutNew where the user runs a never before
seen route
**/
operations

	--WorkoutNew's constructor
	public WorkoutNew: Types`Timestamp * Types`Route ==> WorkoutNew
	WorkoutNew(t, r) == (
		threshold := 10;
		route:= r;
		atomic (
		startTime:= t;
		endTime := t
		);
		return self
	)
	post startTime = endTime and startTime = t;
	
	--Checks if a new Workout is finished which is always true
	public isFinished: () ==> bool
	isFinished() == 
	(
		return len route.route > 1;
	);
	
	--Returns the last point
	public getLastPoint: () ==> [Types`Point]
	getLastPoint() ==
	(
		if len route.route = 0 then
			return nil
		else
			return route.route(len route.route)
	);
	
	--Receives a new GPS Position
	public newGPSPosition: Types`Point ==> ()
	newGPSPosition(point) == 
	(
		if outsideOfThreshold(point) then
			route.route := route.route ^ [point]
		elseif len route.route = 0 then
			(route.route := route.route ^ [point]; 
			return;);	
	)
	post len route.route >= len route~.route;
	
	--Retrieves the total traveled distance
	public getDistance: () ==> nat
	getDistance() == 
	(
		dcl tmp : [Types`Point] := nil;
		dcl distance: real := 0;
		if len route.route <= 1 then
			return distance;
		
		tmp := hd route.route;
		
		for point in tl route.route do
			(distance := distance + Math`distance(tmp, point);
			tmp := point;
			);
			
		return distance;
	)
	post route.route = route~.route;
	
end WorkoutNew